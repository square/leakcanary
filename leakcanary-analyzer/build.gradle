version rootProject.ext.VERSION_NAME
group rootProject.ext.GROUP

buildscript {
  repositories {
    mavenCentral()
  }
}

apply plugin: 'com.android.library'

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.squareup.haha:haha:1.3'
  compile project(':leakcanary-watcher')
  testCompile 'junit:junit:4.12'
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
    return; // Skip debug builds.
  }
  def task = project.tasks.create "jar${name.capitalize()}", Jar
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  artifacts.add('archives', task);
}


// See: https://code.google.com/p/android/issues/detail?id=64887#c13
task copyTestResources(type: Copy) {
  from "${projectDir}/src/test/resources"
  into "${buildDir}/classes/test"
}

afterEvaluate { project ->
  testDebug.dependsOn copyTestResources
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion
  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
  }
}

task sourceJar(type: Jar) {
  from 'src/main/java'
}

apply from: rootProject.file('gradle/checkstyle.gradle')
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
