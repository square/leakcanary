apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
    'minSdk': 14,
    'compileSdk': 27,
  ]
  repositories {
    jcenter()
    google()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.1.0'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  repositories {
    jcenter()
    google()
//    maven {
//      url 'https://oss.sonatype.org/content/repositories/snapshots/'
//    }
//    mavenLocal()
  }

  apply plugin: 'net.ltgt.errorprone'

  tasks.withType(JavaCompile) {
    options.compilerArgs += [
      '-Xlint:all',
      '-Xlint:-serial',
      '-Xlint:-deprecation',
      '-Werror'
    ]
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all the error-prone dependencies to use the same version.
        if (details.requested.group == 'com.google.errorprone' &&
                details.requested.name.startsWith('error_prone_')) {
          details.useVersion '2.1.2'
        }
      }
    }
  }

  tasks.withType(Test) {
    testLogging {
      exceptionFormat 'FULL'
      showCauses true
      showExceptions true
      showStackTraces true
    }
  }
}
