version rootProject.ext.VERSION_NAME
group rootProject.ext.GROUP

buildscript {
  repositories {
    mavenCentral()
  }
}

apply plugin: 'com.android.library'

repositories {
  mavenCentral()
}

dependencies {
  compile project(':leakcanary-analyzer')
  compile "com.android.support:support-v4:22.1.1"
}

def gitSha() {
  return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion
  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    buildConfigField "String", "LIBRARY_VERSION", "\"${rootProject.ext.VERSION_NAME}\""
    buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
    consumerProguardFiles 'consumer-proguard-rules.pro'
  }
}

task sourceJar(type: Jar) {
  from 'src/main/java'
}

apply from: rootProject.file('gradle/checkstyle.gradle')
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')